apply from: "$rootDir/java.gradle"
apply from: "$rootDir/pmd.gradle"
apply from: "$rootDir/intellij.gradle"

sourceSets {
    main {
        ext.originalJavaSrcDirs = java.srcDirs
        ext.originalJavaResourceDirs = resources.srcDirs
        java {
            srcDirs += ['src/metadata/main/java']
        }
        resources {
            srcDirs += ['src/metadata/main/resources']
        }
    }
}

dependencies {
    compile project(':code-generator-api')
    compile project(':code-generator')
}


task generateExampleDataSourceCode(type: JavaExec, dependsOn: 'classes') {
    group = 'Code Generation'
    description = 'Generate code for the example-data layers main and test classes.'
    ext.generatedCodeDestinationDir = file("${project(':example-data').projectDir}/src/generated")
    ext.mainSourceDestinationDir = file(generatedCodeDestinationDir.absolutePath + "/main/java")
    ext.testSourceDestinationDir = file(generatedCodeDestinationDir.absolutePath + "/test/java")

    classpath = sourceSets.main.runtimeClasspath
    main = 'ch.johannes.example.generator.CodeGeneratorRunnable'
    args mainSourceDestinationDir.absolutePath, testSourceDestinationDir.absolutePath

    //support for incremental build support
    inputs.dir sourceSets.main.runtimeClasspath
    outputs.dir generatedCodeDestinationDir.absolutePath;

    //to avoid incremental build support for this task
    outputs.dir file("${buildDir}/tmp/apt/d" + Math.random()) //sourceDestDir.absolutePath;

}