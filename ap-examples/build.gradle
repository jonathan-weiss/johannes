apply from: "$rootDir/java.gradle"
apply from: "$rootDir/intellij.gradle"


rootProject.ext {
    javaLanguageLevel = '1.8'
    generatedApClassesDir = "${buildDir}/generated-src/myclasses"
}


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.9"
    }
}

apply plugin: "net.ltgt.apt"


configurations {
    myGenerator
}


dependencies {
    compile project(':ap')
    myGenerator project(':ap')
}

sourceSets.main {
    ext.originalJavaSrcDirs = java.srcDirs
    java.srcDir "${generatedApClassesDir}"
}

task generateMainClasses(type: JavaCompile) {
    ext.aptDumpDir = file("${buildDir}/tmp/apt")
    destinationDir = aptDumpDir

    classpath = compileJava.classpath + configurations.myGenerator

    source = sourceSets.main.originalJavaSrcDirs
    ext.sourceDestDir = file("$generatedApClassesDir")

    options.define(
            compilerArgs: [
                    //"-nowarn",
                    "-proc:only",
                    "-encoding", "UTF-8",
                    "-processor", "ch.johannes.annotations.MyAnnotationProcessor",
                    "-s", sourceDestDir.absolutePath,
                    "-source", rootProject.javaLanguageLevel,
                    "-target", rootProject.javaLanguageLevel,
            ]
    );
    inputs.dir source
    outputs.dir generatedApClassesDir;

    doFirst {
        sourceDestDir.mkdirs()
    }
    doLast {
        aptDumpDir.delete()
    }
}

compileJava.dependsOn(generateMainClasses)